import java.nio.file.Paths

group 'com.czbix.klog'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'kotlin'

mainClassName = "com.czbix.klog.Main"

repositories {
    mavenCentral()
}

task buildSoy(type: JavaExec) {
    description = 'generate Java constants for soy'
    classpath = sourceSets.main.compileClasspath
    main = 'com.google.template.soy.SoyParseInfoGenerator'

    def packageName = "com.czbix.klog.soy"
    def outputPath = "src/main/java/" + packageName.replace('.', '/')
    doFirst {
        delete outputPath
    }

    def templates = fileTree("data/template")
    def templateDirPath = templates.dir.path + '/'
    def basePath = Paths.get(templateDirPath)

    def soyFiles = templates
            .filter { it.isFile() }.files.path
            .collect { basePath.relativize(Paths.get(it)).toString() }
            .sort()
    args = ["--inputPrefix", templateDirPath, "--srcs", String.join(',', soyFiles),
            "--outputDirectory", outputPath,
            "--javaClassNameSource", "filename",
            "--javaPackage", packageName]
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.apache.httpcomponents:httpcore-nio:4.4.4'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.template:soy:2016-01-12'
    compile 'org.xerial:sqlite-jdbc:3.8.11.2'
    compile 'commons-dbutils:commons-dbutils:1.6'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}
